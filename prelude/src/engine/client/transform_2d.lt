export class Transform2D {
    tx: float = 0,
    ty: float = 0,
    sx: float = 1,
    sy: float = 1,

    static new(scale: float, x: float, y: float) -> Self {
        let sx = scale;
        let sy = scale;
        let tx = x;
        let ty = y;

        Self { sx, sy, tx, ty }
    }

    static identity() -> Self {
        Self::new(1, 0, 0)
    }

    clone() -> Self {
        Self { ..self }
    }

    reset() -> Self {
        self.tx = 0;
        self.ty = 0;
        self.sx = 1;
        self.sy = 1;
        self
    }

    apply(x: float, y: float) -> (float, float) {
        (
            x * self.sx + self.tx,
            y * self.sy + self.ty,
        )
    }

    apply_reverse(x: float, y: float) -> (float, float) {
        (
            (x - self.tx) / self.sx,
            (y - self.ty) / self.sy,
        )
    }

    scale(width: float, height: float) -> (float, float) {
        (
            width * self.sx,
            height * self.sy
        )
    }

    scale_reverse(width: float, height: float) -> (float, float) {
        (
            width / self.sx,
            height / self.sy
        )
    }

    multiply(other: Self) -> Self {
        self.tx = self.tx * other.sx + other.tx;
        self.ty = self.ty * other.sy + other.ty;
        self.sx = self.sx * other.sx;
        self.sy = self.sy * other.sy;
        self
    }

    divide(other: Self) -> Self {
        self.tx = (self.tx - other.tx) / other.sx;
        self.ty = (self.ty - other.ty) / other.sy;
        self.sx = self.sx / other.sx;
        self.sy = self.sy / other.sy;
        self
    }
}