export type Function {
    static default() -> This {{
        (i32.const -2147483648)
    }}

    static type_name() -> string {
        #TYPE_NAME
    }
    
    static __none() -> This {{
        (i32.const -2147483648)
    }}

    __is_none() -> bool {{
        (i32.const -2147483648)
        (i32.eq)
    }}

    __hash() -> int {
        (this as int).__hash()
    }

    to_string() -> string {
        if this.__is_none() {
            "none"
        } else {
            "[Function " + (this as int).to_hexa_string() + "]"
        }
    }

    to_debug_string(set: Set<ptr>) -> string {
        this.to_string()
    }

    eq(This) -> bool {{
        i32.eq
    }}

    ne(This) -> bool {{
        i32.ne
    }}

    static __retain(f: This) {
        if ((f as int) & 0x80000000).eqz() {
            let p = f as ptr;

            if mem_retain(p) {
                Map<int, ptr>::__retain(p[0] as Map<int, ptr>);
                retain_closure_args(p[0], p[2]);
            }
        }
    }

    __serialize(buffer: Buffer) {{

    }}

    static __deserialize(buffer: Buffer) -> This? {{
        (i32.const -2147483648)
    }}
}

fn retain_closure_args(int, int) {{
    (call_indirect (type $retain_closure_args_func_type))
}}