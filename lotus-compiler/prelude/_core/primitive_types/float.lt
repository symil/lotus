export type(f32) float {
    __add(other: float) -> float {{ f32.add }}
    __sub(other: float) -> float {{ f32.sub }}
    __mul(other: float) -> float {{ f32.mul }}
    __div(other: float) -> float {{ f32.div }}
    __eq(other: float) -> bool {{ f32.eq }}
    __ne(other: float) -> bool {{ f32.ne }}
    __ge(other: float) -> bool {{ f32.ge }}
    __gt(other: float) -> bool {{ f32.gt }}
    __le(other: float) -> bool {{ f32.le }}
    __lt(other: float) -> bool {{ f32.lt }}

    to_int() -> int {{ i32.trunc_f32_s }}
    to_float() -> float {{ }}
    as_int() -> int {{ i32.reinterpret_f32 }}

    abs() -> float {{ f32.abs }}
    neg() -> float {{ f32.neg }}
    ceil() -> float {{ f32.ceil }}
    floor() -> float {{ f32.floor }}
    trunc() -> float {{ f32.trunc }}
    round() -> float {{ f32.nearest }}
    sqrt() -> float {{ f32.sqrt }}
    min(other: float) -> float {{ f32.min }}
    max(other: float) -> float {{ f32.max }}

    sign() -> int {
        if self < 0f {
            -1
        } else if self > 0f {
            1
        } else {
            0
        }
    }

    static __default() -> float {{
        (f32.const 0)
    }}

    static __none() -> float {{
        (f32.const nan:0x200000)
    }}

    __is_none() -> bool {{
        i32.reinterpret_f32
        (f32.const nan:0x200000)
        i32.reinterpret_f32
        (i32.eq)
    }}

    __hash() -> int {
        self.as_int().__hash()
    }

    is_nan() -> bool {
        !(self == self)
    }

    to_string() -> string {
        if self.__is_none() {
            return "none";
        }

        let result = @alloc(64);

        wasm_call_float_to_string(self, result);

        result as string
    }

    __to_debug_string(set: Set<ptr>) -> string {
        if self.__is_none() {
            return "none";
        }

        self.to_string()
    }

    static __retain(value: Self) {{

    }}

    static __serialize(buffer: Buffer, value: Self) {
        buffer.write(value.as_int());
    }

    static __deserialize(buffer: Buffer) -> float? {
        buffer.read().as_float()
    }

    static __serialize_with_schema(buffer: Buffer, value: Self) {
        buffer.write(TypeSchema::Float.as_int());
        buffer.write(value.as_int());
    }

    static __deserialize_with_schema(buffer: Buffer) -> Self? {
        let result : Self = none;

        match TypeSchema::__from_int(buffer.read()) {
            TypeSchema::Int => {
                result = buffer.read().to_float()
            },
            TypeSchema::Float => {
                result = buffer.read().as_float()
            },
            TypeSchema::String => consume_string(buffer),
            TypeSchema::Object => consume_object(buffer),
        }

        result
    }
}