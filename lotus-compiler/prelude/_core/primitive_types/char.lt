export type(i32) char {
    __ge(other: char) -> bool {{ i32.ge_s }}
    __gt(other: char) -> bool {{ i32.gt_s }}
    __le(other: char) -> bool {{ i32.le_s }}
    __lt(other: char) -> bool {{ i32.lt_s }}

    __eq(other: Self) -> bool {{
        i32.eq
    }}

    __ne(other: Self) -> bool {{
        i32.ne
    }}

    as_int() -> int {{

    }}

    static __default() -> char {{
        (i32.const 0)
    }}

    static __none() -> char {{
        (i32.const -2147483648)
    }}

    __is_none() -> bool {{
        (i32.const -2147483648)
        (i32.eq)
    }}

    __hash() -> int {
        (self as int).__hash()
    }

    to_string() -> string {
        if self.__is_none() {
            return "none";
        }

        let str = string::__create(1);

        str.__set_char(0, self);

        str
    }

    __to_debug_string(set: Set<ptr>) -> string {
        if self.__is_none() {
            return "none";
        }

        let c = self;

        if c == '\n' {
            return "'\\n'";
        } else if c == '\t' {
            return "'\\t'";
        } else if c == '\0' {
            return "'\\0'";
        } else if c == '\'' {
            return "'\\''";
        }
        
        let str = string::__create(3);

        str.__set_char(0, '\'');
        str.__set_char(1, c);
        str.__set_char(2, '\'');

        str
    }

    static __retain(value: Self) {{

    }}

    static __serialize(buffer: Buffer, value: Self) {
        buffer.write(value.as_int());
    }

    static __deserialize(buffer: Buffer) -> char? {
        buffer.read().to_char()
    }

    static __serialize_with_schema(buffer: Buffer, value: Self) {
        buffer.write(TypeSchema::Int.as_int());
        buffer.write(value as int);
    }

    static __deserialize_with_schema(buffer: Buffer) -> Self? {
        let result : Self = none;

        match TypeSchema::__from_int(buffer.read()) {
            TypeSchema::Int => {
                result = buffer.read() as char;
            },
            TypeSchema::Float => consume_float(buffer),
            TypeSchema::String => consume_string(buffer),
            TypeSchema::Object => consume_object(buffer),
        }

        result
    }
}