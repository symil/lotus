export type(i32) Array<T> {
    type Item = T;

    static __create(length: int) -> This {
        let capacity = length.next_power_of_4();
        let header = @alloc(4);
        let body = @alloc(capacity);

        header[0] = body as int;
        header[1] = length;
        header[2] = capacity;

        header as This
    }

    static with_capacity(capacity: int) -> This {
        capacity = capacity.next_power_of_4();
        let header = @alloc(4);
        let body = @alloc(capacity);

        header[0] = body as int;
        header[1] = 0;
        header[2] = capacity;

        header as This
    }

    static from_raw_parts(pointer: Pointer<T>, length: int, capacity: int) -> This {
        let header = @alloc(4);

        header[0] = pointer as int;
        header[1] = length;
        header[2] = capacity;

        header as This
    }

    static default() -> This {
        This::__create(0)
    }

    static type_name() -> string {
        #TYPE_NAME
    }

    static __none() -> This {{
        (i32.const 0)
    }}

    __is_none() -> bool {{
        (i32.eqz)
    }}

    __hash() -> int {
        (this as int).__hash()
    }

    get_iterable_len() -> int {
        this.len()
    }

    get_iterable_ptr() -> Pointer<T> {
        this.body()
    }

    add(other: This) -> This {
        this.concat(other)
    }

    eq(other: This) -> bool {{
        i32.eq
    }}

    ne(other: This) -> bool {{
        i32.ne
    }}

    body() -> Pointer<T> {
        (this as ptr)[0] as Pointer<T>
    }

    len() -> int {
        (this as ptr)[1]
    }

    capacity() -> int {
        (this as ptr)[2]
    }

    get_at(index: int) -> T {
        let length = this.len();

        if index < 0 {
            index = length + index;
        }

        match index < 0 || index >= length {
            true => none,
            false => this.body()[index]
        }
    }

    set_at(index: int, value: T) {
        this.body().set_at(index, value);
    }

    clear() {
        let header = this as ptr;

        header[1] = 0;
    }

    push(value: T) -> This {
        let header = this as ptr;
        let body = this.body();
        let length = this.len();
        let capacity = this.capacity();

        if length == capacity {
            capacity *= 4;

            let new_body = @alloc(capacity) as Pointer<T>;

            for i in 0..length {
                new_body[i] = body[i];
            }

            body = new_body;
            header[0] = body as int;
            header[2] = capacity;
        }

        body[length] = value;
        header[1] = length + 1;

        this
    }

    pop() -> This {
        let header = this as ptr;
        let length = header[1];

        if length > 0 {
            header[1] = length - 1;
        }

        this
    }

    concat(other: This) -> This {
        let new_array = This::__create(this.len() + other.len());
        let new_body = new_array.body();

        let offset = 0;

        for item in this {
            new_body[offset] = item;
            offset += 1;
        }

        for item in other {
            new_body[offset] = item;
            offset += 1;
        }

        new_array
    }

    join(separator: string) -> string {
        let str = "";
        let length = this.len();

        if length > 0 {
            let last_index = length - 1;
            let i = 0;

            for item in this {
                str += item.to_string();

                if i != last_index {
                    str += separator;
                }

                i += 1;
            }
        }

        str
    }

    as_ptr() -> ptr {
        this as ptr
    }

    to_string() -> string {
        if this.__is_none() {
            return "none";
        }

        "[" + #TYPE_SHORT_NAME + " " + (this as int).to_hexa_string() + "]"
    }

    to_debug_string(set: Set<ptr>) -> string {
        if this.__is_none() {
            return "none";
        }

        let length = this.len();

        if length == 0 {
            return "[]";
        }

        let strings : string[] = [];
        let has_line_breaks = false;

        for item in this {
            let str = item.to_debug_string(set);

            if str.contains("\n") {
                has_line_breaks = true;
            }

            strings.push(str);
        }

        match has_line_breaks {
            true => "[\n" + strings.join(",\n").indent(2) + "\n]",
            false => "[ " + strings.join(", ") + " ]"
        }
    }

    __retain() {
        if mem_retain(this.as_ptr()) {
            mem_retain(this.body() as ptr);

            for item in this {
                item.__retain();
            }
        }
    }

    __serialize(buffer: Buffer) {
        if buffer.save_addr(this) {
            buffer.push(this.len());

            for item in this {
                item.__serialize(buffer);
            }
        }
    }

    static __deserialize(buffer: Buffer) -> This? {
        let addr = buffer.read();

        if !addr || addr == 0 {
            return none;
        }

        let array : This = buffer.retrieve(addr);

        if (array) {
            return array;
        }

        let length = buffer.read();
        array = This::with_capacity(length);

        buffer.register(addr, array);

        for i in 0..length {
            array.push(T::__deserialize(buffer));
        }

        array
    }

    filter(callback: fn(T)->bool) -> T[] {
        let result : T[] = [];

        for item in this {
            if callback(item) {
                result.push(item);
            }
        }

        result
    }

    map<U>(callback: fn(T)->U) -> U[] {
        let result : U[] = [];

        for item in this {
            result.push(callback(item));
        }

        result
    }

    reduce<U>(init: U, callback: fn(U, T)->U) -> U {
        let result = init;

        for item in this {
            result = callback(result, item);
        }
        
        result
    }
}