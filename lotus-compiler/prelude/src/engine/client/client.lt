const CLIENT : Object = none;

const VIRTUAL_WIDTH = 1600f;
const VIRTUAL_HEIGHT = 900f;

let COUNT = 0;

export class Client {
    _user: #USER_TYPE = #USER_TYPE::__default(),
    _local_data: #LOCAL_DATA_TYPE = #LOCAL_DATA_TYPE::__default(),
    _buffer: Buffer = Buffer::with_capacity(BIG_BUFFER_SIZE),

    // Network stuff
    _network_events: RawNetworkEvent[] = [],
    _websocket_id: int = none,
    _is_connected: bool = false,
    _local_object_to_remote_addr: Map<Object, int> = none,
    _request_id_counter: Counter = Counter::new(),
    _request_id_to_info: Map<int, RequestInfo> = Map::new(),

    // Rendering stuff
    _draw_primitives: DrawPrimitive[] = [],
    _window_events: Event[] = [],
    _real_width: float = none,
    _real_height: float = none,
    _virtual_width: float = VIRTUAL_WIDTH,
    _virtual_height: float = VIRTUAL_HEIGHT,
    _virtual_to_real_ratio: float = none,
    _cursor_x: float = 0,
    _cursor_y: float = 0,
    _all_views: View[] = [],

    // General stuff
    _frozen: bool = false,
    _start_time: int = @get_current_time(),
    _local_storage_key_prefix: string = "",
    _view_data_store: ViewDataStore = ViewDataStore,

    // Interaction stuff
    _hash_to_view: Map<int, View> = Map::new(),
    _focused_view_hash: int = none,
    _pressed_view_hash: int = none,
    _dragged_view_hash: int = none,
    _focus_chain: View[] = [],
    _hovered_view: View = none,
    _focused_view: View = none,
    _pressed_view: View = none,
    _dragged_view: View = none,
    _drag_start_x: float = 0,
    _drag_start_y: float = 0,

    static start() {
        if !CLIENT {
            set_pointer_version_seed(wasm_get_current_time());
            let client = Client;
            client._start();
            CLIENT = client;
        }
    }

    static get() -> Self {
        CLIENT as Self
    }

    static udpate() {
        let client = Client::get();

        client._update();
        // @log(`Memory usage: ${get_memory_usage()/1000}KB`);
        @retain(client);
        @trigger_garbage_collection();
    }

    user() -> #USER_TYPE {
        self._user
    }

    local_data() -> #LOCAL_DATA_TYPE {
        self._local_data
    }

    views() -> View[] {
        self._all_views
    }

    window_with() -> float {
        self._virtual_width
    }

    window_height() -> float {
        self._virtual_height
    }

    _start() {
        wasm_init_window(VIRTUAL_WIDTH / VIRTUAL_HEIGHT);

        self._websocket_id = wasm_create_websocket();

        let window = #WINDOW_TYPE::__default();
        let event = OnClientStart {
            client: self,
            local_data: self._local_data,
        };

        event.emit([window]);
    }

    _update() {
        self._collect_window_events();
        self._collect_and_process_network_events();
        check self._should_continue_frame();
        self._create_views();
        self._draw_frame();
        self._process_window_events();
    }

    _should_continue_frame() -> bool {
        !self._frozen
    }

    _collect_window_events() {
        self._real_width = wasm_get_window_width();
        self._real_height = wasm_get_window_height();
        self._virtual_to_real_ratio = self._real_width / self._virtual_width;

        self._window_events.clear();
        self._buffer.reset();
        self._buffer.size = wasm_poll_window_events(self._buffer.data, self._buffer.capacity);

        while !self._buffer.is_finished() {
            let window_event = decode_window_event_from_buffer(self._buffer);

            if window_event is RawMouseEvent(mouse_event) {
                mouse_event.x /= self._virtual_to_real_ratio;
                mouse_event.y /= self._virtual_to_real_ratio;
            } else if window_event is RawWheelEvent(wheel_event) {
                wheel_event.x /= self._virtual_to_real_ratio
                wheel_event.y /= self._virtual_to_real_ratio
                // wheel_event.delta_x /= self._virtual_to_real_ratio
                // wheel_event.delta_y /= self._virtual_to_real_ratio
                // wheel_event.delta_z /= self._virtual_to_real_ratio
            }

            self._window_events.push(window_event);
        }
    }

    _collect_and_process_network_events() {
        let processed_requests : RequestInfo[] = [];

        self._buffer.reset();
        self._buffer.size = wasm_poll_network_events(self._buffer.data, self._buffer.capacity);

        while !self._buffer.is_finished() {
            let websocket_id = self._buffer.read();
            let event_kind = NetworkEventKind::__from_int(self._buffer.read());

            if event_kind == NetworkEventKind::Message {
                let payload_size = self._buffer.read();
                let processed_request_count = self._buffer.read();

                if processed_request_count == 0 {
                    self._buffer.reset_serialization_utility();

                    self._user = @deserialize(self._buffer);
                    self._local_object_to_remote_addr = self._buffer.get_local_object_to_remote_addr_map().clone();
                    self._is_connected = true;
                } else {
                    for i in 0..processed_request_count {
                        let request_id = self._buffer.read();
                        let success = self._buffer.read().to_bool();
                        let info = self._request_id_to_info.delete(request_id);

                        if info {
                            info.success = success;
                            processed_requests.push(info);
                        }
                    }
                }
            }
        }

        for info in processed_requests {
            (info.callback)(info.success, self);
        }
    }

    _create_views() {
        COUNT = 0;
        let root_view = #WINDOW_TYPE::__default();

        root_view.set_rect(Rect::from_size(self._virtual_width, self._virtual_height));

        self._all_views.clear();
        self._reset_interactive_views();
        self._render_view(root_view);
        self._detect_hovered_view();
        self._infer_interactive_views();
        self._post_render();
        self._detect_hovered_view();

        // @log(`wasm: ${COUNT}`);
    }

    _reset_interactive_views() {
        self._focus_chain.clear();
        self._hovered_view = none;
        self._focused_view = none;
        self._pressed_view = none;
        self._dragged_view = none;
    }

    _detect_hovered_view() {
        self._hovered_view = self._detect_view_at(self._cursor_x, self._cursor_y);
    }

    _infer_interactive_views() {
        for view in self._all_views {
            let hash = view.get_approximate_hash();

            if hash == self._focused_view_hash {
                self._focused_view = view;
            }

            if hash == self._pressed_view_hash {
                self._pressed_view = view;
            }

            if hash == self._dragged_view_hash {
                self._dragged_view = view;
            }
        }

        if !self._focused_view || self._focused_view.is_disabled() {
            self._focused_view_hash = none;
            self._focused_view = none;
        }

        if !self._pressed_view || self._pressed_view.is_disabled() {
            self._pressed_view_hash = none;
            self._pressed_view = none;
        }

        if !self._dragged_view || self._dragged_view.is_disabled() {
            self._dragged_view_hash = none;
            self._dragged_view = none;
        }
    }

    _set_focused_view(view: View) {
        let hash = view && view.get_approximate_hash();

        self._focused_view_hash = hash;
        self._focused_view = view;
    }

    _set_pressed_view(view: View) {
        let hash = view && view.get_approximate_hash();

        self._pressed_view_hash = hash;
        self._pressed_view = view;
    }

    _set_dragged_view(view: View) {
        let hash = view && view.get_approximate_hash();

        self._dragged_view_hash = hash;
        self._dragged_view = view;
    }

    _render_view(view: View) {
        let event = OnRender {
            client: self,
            user: self._user,
            local_data: self._local_data
        };

        view._client = self;
        event.emit([view]);
        view.finish_layouts();
        self._all_views.push(view);

        for child in view._children {
            self._render_view(child);
        }
    }

    _detect_view_at(x: float, y: float) -> View? {
        let current : View = none;

        for view in self._all_views {
            if view._graphics.detectable && view._rect.contains(x, y) {
                if !current || view._rect.z >= current._rect.z {
                    current = view;
                }
            }
        }

        current
    }

    _post_render() {
        let event = OnPostRender {
            client: self,
            user: self._user,
            local_data: self._local_data,
        };
        let views = self._all_views.reverse();

        event.emit(views);
    }

    _draw_frame() {
        self._draw_primitives.clear();

        let cursor = Cursor::Default;

        for view in self._all_views {
            let is_disabled = view.is_disabled();

            if !is_disabled {
                let is_hovered = view._graphics.detectable && view._rect.contains(self._cursor_x, self._cursor_y);

                if is_hovered {
                    view._graphics.set(view._hovered_graphics);
                }

                if self._focused_view == view {
                    view._graphics.set(view._focused_graphics);
                }

                if is_hovered && view._graphics.cursor {
                    cursor = view._graphics.cursor;
                }
            } else {
                view._graphics.set(view._disabled_graphics);
            }

            let draw_primitive = view._graphics.__to_draw_primitive(view._rect, self._virtual_to_real_ratio);

            self._draw_primitives.push(draw_primitive);
        }

        self._buffer.reset();
        self._buffer.write_unchecked(cursor);
        self._buffer.write_unchecked(self._draw_primitives.len());

        for primitive in self._draw_primitives {
            self._buffer.write_unchecked(primitive as int);
        }

        wasm_draw_frame(self._buffer.data, self._buffer.size);
    }

    _process_window_events() {
        let views = self._all_views.reverse();
        let client_event = ClientEvent {
            client: self,
            user: self._user,
            local_data: self._local_data,
        };

        for event in self._window_events {
            match event {
                RawMouseEvent(mouse_event) => {
                    let hovered_view = self._detect_view_at(mouse_event.x, mouse_event.y).enabled_or_none()
                    let dragged_view = self._dragged_view.enabled_or_none();
                    let pressed_view = self._pressed_view.enabled_or_none();

                    if mouse_event.action == MouseAction::Down {
                        let will_be_focused : View = none;

                        if hovered_view {
                            self._set_pressed_view(hovered_view);

                            let event = OnMouseDown { ..client_event, ..mouse_event, hovered_view };
                            hovered_view.emit_event(event);

                            will_be_focused = event.will_be_focused;
                        }

                        self.focus(will_be_focused);
                    } else if mouse_event.action == MouseAction::Move {
                        if dragged_view {
                            let dx = mouse_event.x - self._drag_start_x;
                            let dy = mouse_event.y - self._drag_start_y;

                            dragged_view.emit_event(OnDragProgress { ..client_event, ..mouse_event, hovered_view, dx, dy });
                        } else if pressed_view {
                            self._set_dragged_view(pressed_view);
                            self._drag_start_x = self._cursor_x;
                            self._drag_start_y = self._cursor_y;

                            pressed_view.emit_event(OnDragStart { ..client_event, ..mouse_event, hovered_view });
                        }
                    } else if mouse_event.action == MouseAction::Up {
                        if dragged_view {
                            dragged_view.emit_event(OnDragEnd { ..client_event, ..mouse_event, hovered_view });
                        }

                        if pressed_view && pressed_view.rect().contains(mouse_event.x, mouse_event.y) {
                            pressed_view.emit_event(OnClick { ..client_event, ..mouse_event, hovered_view });
                        }

                        if hovered_view {
                            hovered_view.emit_event(OnMouseUp { ..client_event, ..mouse_event, hovered_view });
                        }

                        self._set_dragged_view(none);
                        self._set_pressed_view(none);
                    }

                    self._cursor_x = mouse_event.x;
                    self._cursor_y = mouse_event.y;
                },
                RawKeyboardEvent(keyboard_event) => {
                    if keyboard_event.action == KeyboardAction::Down {
                        OnKeyDown { ..client_event, ..keyboard_event }.emit(views)
                    } else if keyboard_event.action == KeyboardAction::Up {
                        OnKeyUp { ..client_event, ..keyboard_event }.emit(views)
                    }
                },
                RawWheelEvent(wheel_event) => {
                    let view = self._detect_view_at(wheel_event.x, wheel_event.y);

                    view.emit_event(OnScroll { ..client_event, ..wheel_event });
                }
            }
        }
    }

    send_request(request: Object) {
        self.send_request_with_callback(request, none);
    }

    send_request_with_callback(request: Object, callback: RequestCallback) {
        let request_id = self._request_id_counter.next();

        self._buffer.reset();
        self._buffer.write(request_id);
        self._buffer.set_local_object_to_remote_addr_map(self._local_object_to_remote_addr);
        @serialize(request, self._buffer);

        if callback {
            self._request_id_to_info.set(request_id, RequestInfo {
                request,
                callback,
            });
        }

        wasm_send_message(self._websocket_id, self._buffer.data, self._buffer.size);
    }

    hovered_view() -> View? {
        self._hovered_view
    }

    add_to_focus_chain(view: View) {
        self._focus_chain.push(view);
    }

    clear_focus_chain() {
        self.set_focus_chain([]);
    }

    set_focus_chain(views: View[]) {
        self.focus(none);
        self._focus_chain = views;
    }

    get_focus_chain() -> View[] {
        self._focus_chain.clone()
    }

    focus(view: View?) {
        if self._focused_view == view {
            return;
        }

        self._set_focused_view(view);

        if (view) {
            OnFocus {
                client: self,
                user: self._user,
                local_data: self._local_data,
                view
            }.emit([view]);
        }
    }

    focused_view() -> View? {
        self._focused_view
    }

    _focus_next_or_prev(d: int) {
        let current_index = self._focus_chain.index_of(self._focused_view);
        let start_index = current_index || (d * -1);
        let chain_length = self._focus_chain.len();

        for i in 0..chain_length {
            let index = (start_index + chain_length + d + i * d) % chain_length;
            let view = self._focus_chain[index];

            if !view.is_disabled() {
                self.focus(view);
                return;
            }
        }
    }

    focus_next() {
        self._focus_next_or_prev(1);
    }

    focus_prev() {
        self._focus_next_or_prev(-1);
    }

    set_local_storage_key_prefix(prefix: string) {
        self._local_storage_key_prefix = prefix;
    }

    set_local_storage_item<T>(key: string, data: T) {
        key = self._local_storage_key_prefix + key;
        self._buffer.reset();
        T::__serialize_with_schema(self._buffer, data);
        wasm_set_local_storage_item(key, self._buffer.data, self._buffer.size);
    }

    remove_local_storage_item(key: string) {
        key = self._local_storage_key_prefix + key;
        wasm_remove_local_storage_item(key);
    }

    get_local_storage_item<T>(key: string) -> T? {
        key = self._local_storage_key_prefix + key;
        self._buffer.reset();
        self._buffer.size = wasm_get_local_storage_item(key, self._buffer.data, self._buffer.capacity);
        T::__deserialize_with_schema(self._buffer)
    }

    clear_local_storage() {
        wasm_clear_local_storage();
    }

    set_window_title(title: string) {
        wasm_set_window_title(title);
    }

    freeze() {
        self._frozen = true;
    }

    unfreeze() {
        self._frozen = false;
    }

    get_current_time() -> float {
        (@get_current_time() - self._start_time).to_float() / 1000f
    }

    prompt(message: string) -> string {
        self._buffer.reset();
        wasm_prompt(message, self._buffer);
        self._buffer.read_string()
    }

    get_key_value(key: Key) -> string {
        self._buffer.reset();
        wasm_get_key_value(key, self._buffer);
        self._buffer.read_string()
    }

    _get_string_from_js(call: fn(Buffer)) -> string {
        self._buffer.reset();
        call(self._buffer);
        self._buffer.read_string()
    }

    get_href() -> string {
        self._get_string_from_js(buffer => wasm_get_href(buffer))
    }

    get_hostname() -> string {
        self._get_string_from_js(buffer => wasm_get_hostname(buffer))
    }

    get_protocol() -> string {
        self._get_string_from_js(buffer => wasm_get_protocol(buffer))
    }

    get_view_local_data<T : Object>(view: View) -> T {
        self._view_data_store.get(view)
    }

    dyn __allows_network_serialization(user: #USER_TYPE) -> bool {
        false
    }

    dyn __to_debug_string_dyn(set: Set<int>) -> string{
        "<CLIENT>"
    }
}

sys fn start_client() {
    Client::start()
}

sys fn update_client() {
    Client::udpate()
}