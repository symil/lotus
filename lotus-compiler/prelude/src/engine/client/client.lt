const CLIENT : Object = none;

const VIRTUAL_WIDTH = 1600f;
const VIRTUAL_HEIGHT = 900f;

export class Client {
    _user: #USER_TYPE = #USER_TYPE::__default(),
    _local_data: #LOCAL_DATA_TYPE = #LOCAL_DATA_TYPE::__default(),
    _window: #WINDOW_TYPE = #WINDOW_TYPE::__default(),
    _buffer: Buffer = Buffer::with_capacity(BIG_BUFFER_SIZE),

    // Network stuff
    _network_events: RawNetworkEvent[] = [],
    _websocket_id: int = none,
    _is_connected: bool = false,
    _local_object_to_remote_addr: Map<Object, int> = none,
    _request_id_counter: Counter = Counter::new(),
    _request_id_to_info: Map<int, RequestInfo> = Map::new(),

    // Rendering stuff
    _draw_primitives: DrawPrimitive[] = [],
    _window_events: Event[] = [],
    _real_width: float = none,
    _real_height: float = none,
    _virtual_width: float = VIRTUAL_WIDTH,
    _virtual_height: float = VIRTUAL_HEIGHT,
    _virtual_to_real_ratio: float = none,
    _window_rect: Rect = Rect::from_size(VIRTUAL_WIDTH, VIRTUAL_HEIGHT),
    _cursor_position: Vector = Vector,
    _all_views: View[] = [],
    _object_to_view: Map<Object, View> = Map::new(),

    // General stuff
    _frozen: bool = false,
    _start_time: int = @get_current_time(),
    _local_storage: LocalStorage = LocalStorage,

    // Interaction stuff
    _focus_chain: Object[] = [],
    _hash_to_view: Map<int, View> = Map::new(),
    _hovered_view_stack: HoveredViewStack = HoveredViewStack,
    _focused_view_stack: FocusedViewStack = FocusedViewStack,
    _pressed_view_stack: PressedViewStack = PressedViewStack,
    _dragged_view_stack: DraggedViewStack = DraggedViewStack,
    _press_position: Vector = none,
    _dragging: bool = false,

    static start() {
        if !CLIENT {
            set_pointer_version_seed(wasm_get_current_time());
            let client = Client;
            client._start();
            CLIENT = client;
        }
    }

    static get() -> Self {
        CLIENT as Self
    }

    static udpate() {
        let client = Client::get();

        client._update();
        // @log(`Memory usage: ${get_memory_usage()/1000}KB`);
        @retain(client);
        @trigger_garbage_collection();
    }

    user() -> #USER_TYPE {
        self._user
    }

    local_data() -> #LOCAL_DATA_TYPE {
        self._local_data
    }

    views() -> View[] {
        self._all_views
    }

    window_with() -> float {
        self._virtual_width
    }

    window_height() -> float {
        self._virtual_height
    }

    _start() {
        wasm_init_window(VIRTUAL_WIDTH / VIRTUAL_HEIGHT);

        self._websocket_id = wasm_create_websocket();

        let event = OnClientStart {
            client: self,
            local_data: self._local_data,
        };

        event.emit([self._window]);
    }

    _update() {
        self._collect_window_events();
        self._collect_and_process_network_events();
        check self._should_continue_frame();
        self._trigger_client_update();
        check self._should_continue_frame();
        self._create_views();
        self._draw_frame();
        self._process_window_events();
    }

    _trigger_client_update() {
        OnClientUpdate { client: self, user: self._user, local_data: self._local_data }.emit([self._window])
    }

    _should_continue_frame() -> bool {
        !self._frozen
    }

    _collect_window_events() {
        self._real_width = wasm_get_window_width();
        self._real_height = wasm_get_window_height();
        self._virtual_to_real_ratio = self._real_width / self._virtual_width;

        self._window_events.clear();
        self._buffer.reset();
        self._buffer.size = wasm_poll_window_events(self._buffer.data, self._buffer.capacity);

        while !self._buffer.is_finished() {
            let window_event = decode_window_event_from_buffer(self._buffer);

            if window_event is RawMouseEvent(mouse_event) {
                mouse_event.x /= self._virtual_to_real_ratio;
                mouse_event.y /= self._virtual_to_real_ratio;
            } else if window_event is RawWheelEvent(wheel_event) {
                wheel_event.x /= self._virtual_to_real_ratio
                wheel_event.y /= self._virtual_to_real_ratio
                // wheel_event.delta_x /= self._virtual_to_real_ratio
                // wheel_event.delta_y /= self._virtual_to_real_ratio
                // wheel_event.delta_z /= self._virtual_to_real_ratio
            }

            self._window_events.push(window_event);
        }
    }

    _collect_and_process_network_events() {
        let processed_requests : RequestInfo[] = [];

        self._buffer.reset();
        self._buffer.size = wasm_poll_network_events(self._buffer.data, self._buffer.capacity);

        while !self._buffer.is_finished() {
            let websocket_id = self._buffer.read();
            let event_kind = NetworkEventKind::__from_int(self._buffer.read());

            if event_kind == NetworkEventKind::Message {
                let payload_size = self._buffer.read();
                let processed_request_count = self._buffer.read();

                if processed_request_count == 0 {
                    self._buffer.reset_serialization_utility();

                    self._user = @deserialize(self._buffer);
                    self._local_object_to_remote_addr = self._buffer.get_local_object_to_remote_addr_map().clone();

                    if !self._is_connected {
                        self._is_connected = true;
                        // let window = #WINDOW_TYPE::__default();
                        // OnClientConnect { client: self, user: self._user, local_data: self._local_data }.emit([window]);
                    }

                } else {
                    for i in 0..processed_request_count {
                        let request_id = self._buffer.read();
                        let success = self._buffer.read().to_bool();
                        let info = self._request_id_to_info.delete(request_id);

                        if info {
                            info.success = success;
                            processed_requests.push(info);
                        }
                    }
                }
            }
        }

        for info in processed_requests {
            (info.callback)(info.success, self);
        }
    }

    _create_views() {
        self._window = #WINDOW_TYPE::__default();

        self._reset_views();
        self.render(self._window);
        self._infer_interactive_views();
        self._post_render();
        self._infer_interactive_views();

        // @log(`wasm: ${COUNT}`);
    }

    _reset_views() {
        self._all_views.clear();
        self._object_to_view.clear();
        self._focus_chain.clear();
        self._hovered_view_stack.clear();
        self._focused_view_stack.clear();
        self._pressed_view_stack.clear();
        self._dragged_view_stack.clear();
    }

    _detect_hovered_views() {
        let x = self._cursor_position.x;
        let y = self._cursor_position.y;
        let stack : View[] = [];

        for view in self._all_views {
            let hovered = view.detectable && !view.disabled && view.rect.contains(x, y);

            if hovered {
                stack.push(view);
            }
        }

        stack.reverse_in_place()
            .sort_in_place((a, b) => (a.rect.z - b.rect.z).to_int());
        
        self._hovered_view_stack.set_from_array(stack);
    }

    _infer_interactive_views() {
        self._detect_hovered_views();
        self._focused_view_stack.fill_from_hashes(self._all_views);
        self._pressed_view_stack.fill_from_hashes(self._all_views);
        self._dragged_view_stack.fill_from_hashes(self._all_views);
    }

    _post_render() {
        let event = OnPostRender {
            client: self,
            user: self._user,
            local_data: self._local_data,
        };
        let views = self._all_views.reverse();

        event.emit(views);
    }

    _draw_frame() {
        self._draw_primitives.clear();

        let cursor = Cursor::Default;

        for view in self._all_views {
            if view.disabled {
                view._graphics.set(view._disabled_graphics);
            }

            if view.focused {
                view._graphics.set(view._focused_graphics);
            }

            if view.hovered {
                view._graphics.set(view._hovered_graphics);

                if view._graphics.cursor {
                    cursor = view._graphics.cursor;
                }
            }

            let draw_primitive = view._graphics.__to_draw_primitive(view.rect, self._virtual_to_real_ratio);

            self._draw_primitives.push(draw_primitive);
        }

        self._buffer.reset();
        self._buffer.write_unchecked(cursor);
        self._buffer.write_unchecked(self._draw_primitives.len());

        for primitive in self._draw_primitives {
            self._buffer.write_unchecked(primitive as int);
        }

        wasm_draw_frame(self._buffer.data, self._buffer.size);
    }

    _process_window_events() {
        let views = self._all_views.reverse();
        let client_event = ClientEvent {
            client: self,
            user: self._user,
            local_data: self._local_data,
        };

        for event in self._window_events {
            match event {
                RawMouseEvent(mouse_event) => {
                    self._cursor_position.set(mouse_event.x, mouse_event.y)
                    self._detect_hovered_views();

                    if mouse_event.action == MouseAction::Down {
                        let event = OnMouseDown { ..client_event, ..mouse_event };

                        self._pressed_view_stack.set_from_stack(self._hovered_view_stack);
                        event.emit(self._hovered_view_stack.list());
                        self.focus(event.will_be_focused);
                    } else if mouse_event.action == MouseAction::Move {
                        if self._press_position {
                            if self._dragging {
                                let (dx, dy) = self._cursor_position - self._press_position;

                                OnDragProgress { ..client_event, ..mouse_event, dx, dy }.emit(self._dragged_view_stack.list());
                            } else {
                                self._dragged_view_stack.set_from_stack(self._hovered_view_stack);
                                self._press_position = self._cursor_position.clone();

                                OnDragStart { ..client_event, ..mouse_event }.emit(self._dragged_view_stack.list())
                            }
                        }
                    } else if mouse_event.action == MouseAction::Up {
                        if self._press_position && self._dragging {
                            OnDragEnd { ..client_event, ..mouse_event }.emit(self._dragged_view_stack.list());
                        }

                        let clicked_views = self._pressed_view_stack.list().filter(view => view.rect.contains(mouse_event.x, mouse_event.y));

                        OnClick { ..client_event, ..mouse_event }.emit(clicked_views);
                        OnMouseUp { ..client_event, ..mouse_event }.emit(self._hovered_view_stack.list());

                        self._dragging = false;
                        self._press_position = none;
                        self._pressed_view_stack.clear();
                        self._dragged_view_stack.clear();
                    }
                },
                RawKeyboardEvent(keyboard_event) => {
                    if keyboard_event.action == KeyboardAction::Down {
                        OnKeyDown { ..client_event, ..keyboard_event }.emit(views)
                    } else if keyboard_event.action == KeyboardAction::Up {
                        OnKeyUp { ..client_event, ..keyboard_event }.emit(views)
                    }
                },
                RawWheelEvent(wheel_event) => {
                    self._cursor_position.set(wheel_event.x, wheel_event.y);
                    self._detect_hovered_views();

                    OnScroll { ..client_event, ..wheel_event }.emit(self._hovered_view_stack.list());
                }
            }
        }
    }

    render(object: Object, rect: Rect = none) {
        check object;

        let view = View.set_rect(rect || self._window_rect);
        let render_event = OnRender {
            client: self,
            user: self._user,
            local_data: self._local_data,
            view: View
        };

        let view = render_event.emit([object]).view;

        if view {
            view.finish_layouts();
            view.data = object;

            self._object_to_view.set(object, view);
            self._all_views.push(view);
        }
    }

    get_view(item: Object) -> View {
        self._object_to_view.get(item)
    }

    send_request(request: Object) {
        self.send_request_with_callback(request, none);
    }

    send_request_with_callback(request: Object, callback: RequestCallback) {
        let request_id = self._request_id_counter.next();

        self._buffer.reset();
        self._buffer.write(request_id);
        self._buffer.set_local_object_to_remote_addr_map(self._local_object_to_remote_addr);
        @serialize(request, self._buffer);

        if callback {
            self._request_id_to_info.set(request_id, RequestInfo {
                request,
                callback,
            });
        }

        wasm_send_message(self._websocket_id, self._buffer.data, self._buffer.size);
    }

    focus(object: Object) {
        let view = self._object_to_view.get(object);

        if self._focused_view == view {
            return;
        }

        self._set_focused_view(view);

        if (view) {
            OnFocus {
                client: self,
                user: self._user,
                local_data: self._local_data,
                view
            }.emit([view.data]);
        }
    }

    clear_focus() {
        self.focus(none);
    }

    focused_item() -> Object {
        self._focused_view.and_then(view => view.data)
    }

    is_focused(item: Object) -> bool {
        self.focused_item() == item
    }

    add_to_focus_chain(object: Object) {
        self._focus_chain.push_if_not_none(object);
    }

    clear_focus_chain() {
        self.set_focus_chain([]);
    }

    set_focus_chain(chain: Object[]) {
        self.focus(none);
        self._focus_chain = chain;
    }

    get_focus_chain() -> Object[] {
        self._focus_chain.clone()
    }

    _focus_next_or_prev(d: int) {
        let current_index = self._focus_chain.index_of(self._focused_view);
        let start_index = current_index || (d * -1);
        let chain_length = self._focus_chain.len();

        for i in 0..chain_length {
            let index = (start_index + chain_length + d + i * d) % chain_length;
            let object = self._focus_chain[index];
            let view = self._object_to_view.get(object);

            if !view.is_disabled() {
                self.focus(object);
                return;
            }
        }
    }

    focus_next() {
        self._focus_next_or_prev(1);
    }

    focus_prev() {
        self._focus_next_or_prev(-1);
    }

    set_window_title(title: string) {
        wasm_set_window_title(title);
    }

    freeze() {
        self._frozen = true;
    }

    unfreeze() {
        self._frozen = false;
    }

    get_current_time() -> float {
        (@get_current_time() - self._start_time).to_float() / 1000f
    }

    prompt(message: string) -> string {
        self._buffer.reset();
        wasm_prompt(message, self._buffer);
        self._buffer.read_string()
    }

    get_key_value(key: Key) -> string {
        self._buffer.reset();
        wasm_get_key_value(key, self._buffer);
        self._buffer.read_string()
    }

    _get_string_from_js(call: fn(Buffer)) -> string {
        self._buffer.reset();
        call(self._buffer);
        self._buffer.read_string()
    }

    get_href() -> string {
        self._get_string_from_js(buffer => wasm_get_href(buffer))
    }

    get_hostname() -> string {
        self._get_string_from_js(buffer => wasm_get_hostname(buffer))
    }

    get_protocol() -> string {
        self._get_string_from_js(buffer => wasm_get_protocol(buffer))
    }

    get_view_local_data<T : Object>(view: View) -> T {
        self._view_data_store.get(view)
    }

    // LOCAL STORAGE API
    set_local_storage_key_prefix(prefix: string) {
        self._local_storage.set_key_prefix(prefix);
    }

    get_local_storage_item<T>(key: string) -> T {
        self._local_storage.get_item(key)
    }

    set_local_storage_item<T>(key: string, data: T) {
        self._local_storage.set_item(key, data);
    }

    remove_local_storage_item(key: string) {
        self._local_storage.remove_item(key);
    }

    clear_local_storage() {
        self._local_storage.clear();
    }

    // OVERRIDE DYNAMIC METHODS
    dyn __allows_network_serialization(user: #USER_TYPE) -> bool {
        false
    }

    dyn __to_debug_string_dyn(set: Set<int>) -> string{
        "<CLIENT>"
    }
}

sys fn start_client() {
    Client::start()
}

sys fn update_client() {
    Client::udpate()
}