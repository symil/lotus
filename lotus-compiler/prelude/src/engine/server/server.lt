const SERVER : Object = none;

pub class Server {
    _buffer: Buffer = Buffer::with_capacity(BIG_BUFFER_SIZE),
    _world: #WORLD_TYPE = none,
    _websocket_server_id: int = none,
    _user_to_metadata: Map<#USER_TYPE, UserMetadata> = Map::new(),
    _websocket_id_to_user: Map<int, #USER_TYPE> = Map::new(),
    _websocket_id_counter: Counter = Counter,
    _request_to_request_id: Map<Object, int> = Map::new(),
    _start_time: int = @get_current_time(),
    _last_update_time: float = 0,
    _objects_to_update: Set<Object> = Set::new(),

    static start() {
        if !SERVER {
            set_memory_block_version_seed(wasm_get_current_time());
            let server = Server;
            server._start();
            SERVER = server;
        }
    }

    static get() -> Self {
        SERVER as Server
    }

    static update() {
        let server = Server::get();

        server._update();
        @retain(server);
        @trigger_garbage_collection();
    }

    users() -> #USER_TYPE[] {
        self._user_to_metadata.keys()
    }

    get_current_time() -> float {
        (@get_current_time() - self._start_time).to_float() / 1000f
    }

    _start() {
        self._last_update_time = self.get_current_time();
        self._websocket_server_id = wasm_create_websocket_server();
        self._world = #WORLD_TYPE::__default();

        @emit(OnWorldOpen { server: self, world: self._world }, [self._world]);
    }

    _update() {
        self._process_network_events();
        self._update_world();
        self._update_users();
    }

    _update_world() {
        let current_time = self.get_current_time();
        let elapsed = current_time - self._last_update_time;

        OnWorldUpdate { server: self, world: self._world, elapsed }.emit([self._world]);

        for object in self._objects_to_update.values() {
            OnWorldUpdate { server: self, world: self._world, elapsed }.emit([object]);
        }

        self._last_update_time = current_time;
    }

    _process_network_events() {
        self._buffer.reset();
        self._buffer.size = wasm_poll_network_events(self._buffer.data, self._buffer.capacity);

        let events_to_emit : UserEvent[] = [];

        while !self._buffer.is_finished() {
            let websocket_id = self._buffer.read();
            let event_kind = NetworkEventKind::__from_int(self._buffer.read());

            match event_kind {
                NetworkEventKind::ConnectionOpen => {
                    let user = #USER_TYPE::__default();
                    let metadata = UserMetadata {
                        websocket_id,
                    };

                    self._websocket_id_to_user.set(websocket_id, user);
                    self._user_to_metadata.set(user, metadata);

                    events_to_emit.push(OnUserConnect { server: self, world: self._world, user });
                },
                NetworkEventKind::ConnectionClosed => {
                    let user = self._websocket_id_to_user.get(websocket_id);

                    self._websocket_id_to_user.delete(websocket_id);
                    self._user_to_metadata.delete(user);

                    events_to_emit.push(OnUserDisconnect { server: self, world: self._world, user });
                },
                NetworkEventKind::Message => {
                    self._buffer.reset_serialization_utility();

                    let user = self._websocket_id_to_user.get(websocket_id);
                    let payload_size = self._buffer.read();
                    let request_id = self._buffer.read();
                    let request : Object = @deserialize(self._buffer);

                    if request {
                        self._request_to_request_id.set(request, request_id);
                        events_to_emit.push(OnUserRequest { server: self, world: self._world, user, request });
                    }
                }
            }
        }

        for event in events_to_emit {
            let initial_user = event.user;

            @emit(event, [self._world]);

            let new_user = event.user;

            if initial_user != new_user {
                let metadata = self._user_to_metadata.get(initial_user);

                self._user_to_metadata.set(new_user, metadata);
                self._user_to_metadata.delete(initial_user);
                self._websocket_id_to_user.set(metadata.websocket_id, new_user);
            }

            if event is OnUserRequest(on_user_request) {
                let request_id = self._request_to_request_id.delete(on_user_request.request);
                let metadata = self._user_to_metadata.get(on_user_request.user);
                let success = on_user_request.success;

                if request_id && metadata {
                    metadata.processed_requests.push(RequestResult {
                        request_id,
                        success,
                    });
                }
            }
        }
    }

    _update_users() {
        for entry in self._user_to_metadata {
            let user = entry.key;
            let metadata = entry.value;

            if user {
                if !metadata.processed_requests.is_empty() {
                    self._buffer.reset();
                    self._buffer.write(metadata.processed_requests.len());

                    for request_result in metadata.processed_requests {
                        self._buffer.write(request_result.request_id);
                        self._buffer.write(request_result.success.to_int());
                    }

                    metadata.processed_requests.clear();

                    wasm_send_message(metadata.websocket_id, self._buffer.data, self._buffer.size);
                }

                self._buffer.reset();
                self._buffer.write(0);
                self._buffer.context = user;

                @serialize(user, self._buffer);
                wasm_send_message(metadata.websocket_id, self._buffer.data, self._buffer.size);

                // self._buffer.read_index = 1;
                // let deserialized : #USER_TYPE = @deserialize(self._buffer);
                // if deserialized {
                //     @dbg(deserialized);
                // }
            }
        }
    }

    store<T>(path: string, content: T) {
        self._buffer.reset();
        @serialize(content, self._buffer);
        wasm_write_file(path, self._buffer.data, self._buffer.size);
    }

    load<T>(path: string) -> T {
        self._buffer.reset();
        self._buffer.size = wasm_read_file(path, self._buffer.data, self._buffer.capacity);
        self._buffer.read();

        @deserialize(self._buffer)
    }

    emulate_request(user: #USER_TYPE, request: Object) {
        let server = self;
        let world = self._world;

        OnUserRequest { server, world, user, request }.emit([world]);
    }

    connect_bot(bot: Bot, requests: Object[] = []) -> #USER_TYPE {
        check bot;

        let server = self;
        let world = self._world;
        let user = #USER_TYPE::__default();

        user.bot = bot;
        bot.user = user;
        self._objects_to_update.add(bot);

        OnUserConnect { server, world, user }.emit([world, bot]);

        for request in requests {
            OnUserRequest { server, world, user, request }.emit([world, bot]);
        }

        user
    }

    disconnect_bot(user: #USER_TYPE) {
        check user.bot;

        let server = self;
        let world = self._world;

        OnUserDisconnect { server, world, user }.emit([world, user.bot]);

        self._objects_to_update.delete(user.bot);
    }

    add_object_to_update(object: Object) {
        self._objects_to_update.add(object);
    }

    remove_object_to_update(object: Object) {
        self._objects_to_update.delete(object);
    }

    dyn __allows_network_serialization(user: #USER_TYPE) -> bool {
        false
    }
}

sys fn start_server() {
    Server::start()
}

sys fn update_server() {
    Server::update();
}