export class Array<T> {
    type Item = T;

    _body: Pointer<T>,
    _length: int,
    _capacity: int,

    __add(other: Self) -> Self {
        self.concat(other)
    }

    static __none() -> Self {{
        (i32.const 0)
    }}

    static __default() -> Self {
        Self::with_capacity(4)
    }

    static with_capacity(capacity: int) -> Self {
        Self {
            _body: @alloc(capacity) as Pointer<T>,
            _length: 0,
            _capacity: capacity.next_power_of_4()
        }
    }

    static from_raw_parts(body: Pointer<T>, length: int, capacity: int) -> Self {
        Self {
            _body: body,
            _length: length,
            _capacity: capacity
        }
    }

    body() -> Pointer<T> {
        self._body
    }

    len() -> int {
        self._length
    }

    capacity() -> int {
        self._capacity
    }

    __get_iterable_len() -> int {
        self._length
    }

    __get_iterable_ptr() -> Pointer<T> {
        self._body
    }

    __get_at_index(index: int) -> T {
        let length = self._length;

        if index < 0 {
            index = length + index;
        }

        match index < 0 || index >= length {
            true => none,
            false => self._body[index]
        }
    }

    __set_at_index(index: int, value: T) {
        self._body.__set_at_index(index, value);
    }

    get(index: int) -> T? {
        self.__get_at_index(index)
    }

    first() -> T? {
        if self.is_empty() {
            return none;
        }

        self.__get_at_index(0)
    }

    last() -> T? {
        if self.is_empty() {
            return none;
        }

        self.__get_at_index(self._length - 1)
    }

    is_empty() -> bool {
        self._length == 0
    }

    clone() -> Self {
        let result : Self = [];

        result.extend(self);

        result
    }

    reverse() -> Self {
        for i in 0..(self._length / 2) {
            let i2 = self._length - i - 1;
            let tmp = self._body[i];

            self._body[i] = self._body[i2];
            self._body[i2] = tmp;
        }

        self
    }

    clear() {
        self._length = 0;
    }

    index_of(searched_item: T) -> int? {
        for [i, item] in self {
            if item == searched_item {
                return i;
            }
        }

        none
    }

    includes(searched_item: T) -> bool {
        !!self.index_of(searched_item)
    }

    remove(item: T) -> bool {
        let index = self.index_of(item);

        if index {
            let dest = self._body.add(index);
            let src = dest.add(1);
            let data_size = self._length - index - 1;

            src.copy_to(dest, data_size);
            self._length -= 1;
        }

        !!index
    }

    _add_length(additional_length: int) -> int {
        let current_length = self._length;
        let current_length = self._length;
        let new_length = current_length + additional_length;

        if new_length > self._capacity {
            let current_body = self._body;
            let new_capacity = self._capacity * 4;
            let new_body = @alloc(new_capacity) as Pointer<T>;

            current_body.copy_to(new_body, current_length);

            self._body = new_body;
            self._capacity = new_capacity;
        }

        self._length = new_length;

        current_length
    }

    push(value: T) -> Self {
        let length = self._add_length(1);

        self._body[length] = value;

        self
    }

    push_unchecked(value: T) {
        self._body[self._length] = value;
        self._length += 1;
    }

    pop() -> Self {
        if self._length > 0 {
            self._length -= 1;
        }

        self
    }

    extend(other: Self) {
        let length = self._add_length(other.len());
        let body = self._body;

        for [i, item] in other {
            body[length + i] = item;
        }
    }

    extend_at(index: int, other: Self) {
        if other.is_empty() {
            return;
        }

        self.insert_none(index, other.len());
        let body = self._body;
        
        for [i, item] in other {
            body[index + i] = item;
        }
    }

    insert_at(index: int, item: T) {
        self.insert_none(index, 1);
        self[index] = item;
    }

    insert_none(index: int, count: int) {
        let length = self._add_length(count);
        let body = self._body;

        for i in 0..count {
            let i2 = count - i - 1;
            body[index + count + i2] = body[index + i2];
        }

        for i in 0..count {
            body[index + i] = none;
        }
    }

    concat(other: Self) -> Self {
        let new_array = Self::with_capacity(self._length + other._length);

        for item in self {
            new_array.push_unchecked(item);
        }

        for item in other {
            new_array.push_unchecked(item);
        }

        new_array
    }

    join(separator: string) -> string {
        let str = "";
        let length = self._length;

        if length > 0 {
            let last_index = length - 1;
            let i = 0;

            for item in self {
                str += item.to_string();

                if i != last_index {
                    str += separator;
                }

                i += 1;
            }
        }

        str
    }

    filter(callback: fn(T, int)(bool)) -> T[] {
        let result : T[] = [];

        for [i, item] in self {
            if callback(item, i) {
                result.push(item);
            }
        }

        result
    }

    map<U>(callback: fn(T, int)(U)) -> U[] {
        let result : U[] = [];

        for [i, item] in self {
            result.push(callback(item, i));
        }

        result
    }

    filter_map<U>(callback: fn(T, int)(U)) -> U[] {
        let result : U[] = [];

        for [i, item] in self {
            let mapped = callback(item, i);

            if mapped {
                result.push(mapped);
            }
        }

        result
    }

    reduce<U>(init: U, callback: fn(U, T, int)(U)) -> U {
        let result = init;

        for [i, item] in self {
            result = callback(result, item, i);
        }
        
        result
    }

    sort(compare: fn(T, T)(int)) -> Self {
        let secondary_keys = @alloc(self._length);

        for i in 0..self._length {
            secondary_keys[i] = i;
        }

        quick_sort(self._body, 0, self._length - 1, compare, secondary_keys);

        self
    }

    dyn __retain_dyn() {
        Pointer<T>::__retain(self._body);

        for item in self {
            T::__retain(item);
        }
    }

    dyn __serialize_dyn(buffer: Buffer) {
        buffer.write(self._length);

        for item in self {
            T::__serialize(buffer, item);
        }
    }

    dyn __deserialize_dyn(buffer: Buffer) {
        let length = buffer.read();

        self._body = @alloc(length) as Pointer<T>;
        self._length = 0;
        self._capacity = length.next_power_of_4();

        for i in 0..length {
            self.push_unchecked(T::__deserialize(buffer));
        }
    }

    dyn __serialize_with_schema_dyn(buffer: Buffer) {
        buffer.write(#TYPE_FULL_NAME.__hash());
        buffer.write(self.len());

        for item in self {
            T::__serialize_with_schema(buffer, item);
        }
    }

    dyn __deserialize_with_schema_dyn(buffer: Buffer) {
        let item_count = buffer.read();

        self._capacity = item_count.next_power_of_4();
        self._body = @alloc(self._capacity) as Pointer<T>;
        self._length = item_count;

        for i in 0..item_count {
            let item = T::__deserialize_with_schema(buffer);

            if item is T(casted) {
                self._body[i] = casted;
            } else {
                self._body[i] = none;
            }
        }
    }

    dyn __to_string_dyn() -> string {
        if self.__is_none() {
            return "none";
        }

        "[" + #TYPE_SHORT_NAME + " " + (self as int).to_hexa_string() + "]"
    }

    dyn __to_debug_string_dyn(set: Set<ptr>) -> string {
        let length = self._length;

        if length == 0 {
            return "[]";
        }

        let strings : string[] = [];
        let has_line_breaks = false;

        for item in self {
            let str = item.__to_debug_string(set);

            if str.contains("\n") {
                has_line_breaks = true;
            }

            strings.push(str);
        }

        match has_line_breaks {
            true => "[\n" + strings.join(",\n").indent(2) + "\n]",
            false => "[ " + strings.join(", ") + " ]"
        }
    }
}

fn partition<T>(array: Pointer<T>, start_index: int, end_index: int, compare: fn(T, T)(int), secondary_keys: ptr) -> int {
    let pivot = array[end_index];
    let i = start_index - 1;

    for j in start_index..end_index {
        let compare_value = compare(array[j], pivot);

        if compare_value == 0 {
            compare_value = secondary_keys[j] - secondary_keys[end_index];
        }

        if compare_value < 0 {
            i += 1;
            array.swap(i, j);
            secondary_keys.swap(i, j);
        }
    }

    array.swap(i + 1, end_index);
    secondary_keys.swap(i + 1, end_index);

    i + 1
}

fn quick_sort<T>(array: Pointer<T>, start_index: int, end_index: int, compare: fn(T, T)(int), secondary_keys: ptr) {
    if start_index < end_index {
        let middle_index = partition(array, start_index, end_index, compare, secondary_keys);

        quick_sort(array, start_index, middle_index - 1, compare, secondary_keys);
        quick_sort(array, middle_index + 1, end_index, compare, secondary_keys);
    }
}