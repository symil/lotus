const CLIENT : Client = none;

const VIRTUAL_WIDTH = 1600f;
const VIRTUAL_HEIGHT = 900f;

export class Client {
    user: #USER_TYPE = #USER_TYPE::__default(),
    local_data: #LOCAL_DATA_TYPE = #LOCAL_DATA_TYPE::__default(),

    _buffer: Buffer = Buffer::with_capacity(BIG_BUFFER_SIZE),
    _local_object_to_remote_addr: Map<Object, int> = none,
    _websocket_id: int = none,
    _draw_primitives: DrawPrimitive[] = [],
    _network_events: OnNetworkEvent[] = [],
    _window_events: OnWindowEvent[] = [],
    _is_connected: bool = false,
    _wait_until: fn(Client)(bool) = none,

    _hovered_view: View? = none,
    _focus_chain: View[] = [],
    _focused_index: int? = none,
    _focused_view: View? = none,
    _all_views: View[] = [],
    _real_width: float = none,
    _real_height: float = none,
    _virtual_width: float = VIRTUAL_WIDTH,
    _virtual_height: float = VIRTUAL_HEIGHT,
    _virtual_to_real_ratio: float = none,
    _cursor_x: float = 0,
    _cursor_y: float = 0,

    static get() -> Self {
        CLIENT
    }

    _start() {
        wasm_init_window(VIRTUAL_WIDTH / VIRTUAL_HEIGHT);
        self._websocket_id = wasm_create_websocket(`ws://localhost:${WEBSOCKET_PORT}`);

        let window = #WINDOW_TYPE::__default();
        let event = OnClientStart {
            client: self
        };

        event.emit([window]);
    }

    _update() {
        self._collect_network_events();
        self._collect_window_events();
        self._process_network_events();
        check self._should_continue_frame();
        self._create_views();
        self._draw_frame();
        self._process_window_events();
    }

    _should_continue_frame() -> bool {
        let ok = true;

        if self._wait_until {
            ok = (self._wait_until)(self);

            if ok {
                self._wait_until = none;
            }
        }

        ok
    }

    _collect_network_events() {
        self._network_events.clear();
        self._buffer.reset();
        self._buffer.size = wasm_poll_network_events(self._buffer.data, self._buffer.capacity);

        while !self._buffer.is_finished() {
            self._network_events.push(decode_network_event_from_buffer(self._buffer));
        }
    }

    _collect_window_events() {
        self._real_width = wasm_get_window_width();
        self._real_height = wasm_get_window_height();
        self._virtual_to_real_ratio = self._real_width / self._virtual_width;

        self._window_events.clear();
        self._buffer.reset();
        self._buffer.size = wasm_poll_window_events(self._buffer.data, self._buffer.capacity);

        while !self._buffer.is_finished() {
            let window_event = decode_window_event_from_buffer(self._buffer, self);
            let push = true;

            if window_event is OnMouseEvent(mouse_event) {
                mouse_event.x /= self._virtual_to_real_ratio;
                mouse_event.y /= self._virtual_to_real_ratio;

                self._cursor_x = mouse_event.x;
                self._cursor_y = mouse_event.y;

                if mouse_event.action == MouseAction::Move {
                    push = false;
                }
            }

            if push {
                self._window_events.push(window_event);
            }
        }
    }

    _process_network_events() {
        let last_event = self._network_events.last();

        if last_event && last_event.kind == NetworkEventKind::StateUpdate {
            let buffer = Buffer::from_array(last_event.payload);

            self.user = @deserialize(buffer);
            self._local_object_to_remote_addr = buffer.get_local_object_to_remote_addr_map().clone();
            self._is_connected = true;
        }
    }

    _create_views() {
        let root_view = #WINDOW_TYPE::__default();

        root_view._rect = Rect::from_size(self._virtual_width, self._virtual_height)

        self._all_views.clear();
        self._focus_chain.clear();
        // TODO: clear focused & hovered view?
        self._render_view(root_view);
        self._hovered_view = self._detect_view_at(self._cursor_x, self._cursor_y);
        self._define_interactions();
        self._focused_view = self._focus_chain.get(self._focused_index);

        if !self._focused_view {
            self._focused_index = none;
        }
    }

    _render_view(view: View) {
        let event = OnRender {
            client: self,
        };

        view._client = self;
        event.emit([view]);
        self._all_views.push(view);

        for child in view._children {
            self._render_view(child);
        }
    }

    _detect_view_at(x: float, y: float) -> View? {
        let current : View = none;

        for view in self._all_views {
            if view._graphics.detectable && view._rect.contains(x, y) {
                if !current || view._rect.z >= current._rect.z {
                    current = view;
                }
            }
        }

        current
    }

    _define_interactions() {
        let event = OnPostRender {
            client: self
        };
        let views = self._all_views.clone().reverse();

        event.emit(views);
    }

    _draw_frame() {
        self._draw_primitives.clear();

        let cursor = Cursor::Default;

        for view in self._all_views {
            let is_disabled = view.is_disabled();

            if !is_disabled {
                if self._hovered_view == view {
                    view._graphics.set(view._hovered_graphics);
                }

                if self._focused_view == view {
                    view._graphics.set(view._focused_graphics);
                }

                if self._hovered_view == view {
                    cursor = view._graphics.cursor;
                }
            } else {
                view._graphics.set(view._disabled_graphics);
            }

            let draw_primitive = view._graphics.__to_draw_primitive(view._rect, self._virtual_to_real_ratio);

            self._draw_primitives.push(draw_primitive);

            // if view.graphics.text {
            //     @dbg(draw_primitive);
            // }
        }

        self._buffer.reset();
        self._buffer.write_unchecked(cursor);

        for primitive in self._draw_primitives {
            primitive.write_to_buffer(self._buffer);
        }

        wasm_draw_frame(self._buffer.data, self._buffer.size);
    }

    _process_window_events() {
        let views = self._all_views.clone().reverse();

        for event in self._window_events {
            match event {
                OnMouseEvent(e) => {
                    let view = self._detect_view_at(e.x, e.y);

                    if view && !view.is_disabled() {
                        let callback_list = match e.action {
                            MouseAction::Down => view._on_down_callbacks,
                            MouseAction::Click => view._on_click_callbacks,
                            MouseAction::Up => view._on_up_callbacks,
                            MouseAction::Drag => view._on_drag_callbacks
                        };

                        for callback in callback_list {
                            callback(self);
                        }
                    }
                },
                OnKeyboardEvent(event) => {
                    event.emit(views);
                },
                OnWheelEvent(event) => {
                    event.emit(views);
                }
            }
        }
    }

    send_request<T>(value: T) {
        self._buffer.reset();
        self._buffer.set_local_object_to_remote_addr_map(self._local_object_to_remote_addr);
        @serialize(value, self._buffer);

        wasm_send_message(self._websocket_id, self._buffer.data, self._buffer.size);
    }

    views() -> View[] {
        self._all_views
    }

    get_hovered_view() -> View? {
        self._hovered_view
    }

    add_to_focus_chain(view: View) {
        self._focus_chain.push(view);
    }

    clear_focus_chain() {
        self.set_focus_chain([]);
    }

    set_focus_chain(views: View[]) {
        self.focus(none);
        self._focus_chain = views;
    }

    get_focus_chain() -> View[] {
        self._focus_chain.clone()
    }

    focus(view: View?) {
        if self._focused_view == view {
            return;
        }

        if (view) {
            self._focused_index = self._focus_chain.index_of(view);

            OnFocus { target: view }.emit([view]);
        } else {
            self._focused_index = none;
        }

        self._focused_view = view;
    }

    get_focused_view() -> View? {
        self._focused_view
    }

    _focus_next_or_prev(d: int) {
        let start_index = self._focused_index || (d * -1);
        let chain_length = self._focus_chain.len();

        for i in 0..chain_length {
            let index = (start_index + chain_length + d + i * d) % chain_length;
            let view = self._focus_chain[index];

            if !view.is_disabled() {
                self.focus(view);
                return;
            }
        }
    }

    focus_next() {
        self._focus_next_or_prev(1);
    }

    focus_prev() {
        self._focus_next_or_prev(-1);
    }

    set_local_storage_item<T>(key: string, data: T) {
        self._buffer.reset();
        T::__serialize_with_schema(self._buffer, data);
        wasm_set_local_storage_item(key, self._buffer.data, self._buffer.size);
    }

    remove_local_storage_item(key: string) {
        wasm_remove_local_storage_item(key);
    }

    get_local_storage_item<T>(key: string) -> T? {
        self._buffer.reset();
        self._buffer.size = wasm_get_local_storage_item(key, self._buffer.data, self._buffer.capacity);
        T::__deserialize_with_schema(self._buffer)
    }

    clear_local_storage() {
        wasm_clear_local_storage();
    }

    set_window_title(title: string) {
        wasm_set_window_title(title);
    }

    wait_until(predicate: fn(Client)(bool)) {
        self._wait_until = predicate;
    }

    dyn __allows_network_serialization(user: #USER_TYPE) -> bool {
        false
    }
}

sys fn start_client() {
    CLIENT = Client;
    CLIENT._start();
}

sys fn update_client() {
    CLIENT._update();
    // @log(`memory used: ${get_memory_usage() / 1024} KB`);
    @retain(CLIENT);
    @trigger_garbage_collection();
}