const CLIENT : Client = none;

const VIRTUAL_WIDTH = 1600f;
const VIRTUAL_HEIGHT = 900f;

export class Client {
    buffer: SimpleBuffer,
    real_width: float,
    real_height: float,
    virtual_width: float,
    virtual_height: float,
    virtual_to_real_ratio: float,
    cursor_x: float,
    cursor_y: float,
    websocket_id: int,

    static new() -> Self {
        Self {
            buffer: SimpleBuffer::new(BIG_BUFFER_SIZE),
            virtual_width: VIRTUAL_WIDTH,
            virtual_height: VIRTUAL_HEIGHT
        }
    }

    _start() {
        @log("CLIENT START");

        wasm_init_window(VIRTUAL_WIDTH / VIRTUAL_HEIGHT);
        self.websocket_id = wasm_create_websocket(`ws://localhost:${WEBSOCKET_PORT}`);
    }

    _update() {
        self._process_network_events();
        self._process_window_events();
    }

    _process_window_events() {
        let written_count = wasm_poll_window_events(self.buffer.pointer, self.buffer.size);

        self.buffer.reset_index();

        while self.buffer.index < written_count {
            let window_event = decode_window_event_from_buffer(self.buffer);
        }

        self.real_width = wasm_get_window_width();
        self.real_height = wasm_get_window_height();
        self.virtual_to_real_ratio = self.real_width / self.virtual_width;

        let primitive = DrawPrimitive {
            x: 500,
            y: 300,
            width: 200,
            height: 100,
            background_color: ORANGE
        };

        self.buffer.reset_index();
        self.buffer.write(Cursor::Pointer);
        primitive.write_to_buffer(self.buffer);

        wasm_draw_frame(self.buffer.pointer, self.buffer.index);
    }

    _process_network_events() {
        let written_count = wasm_poll_network_events(self.buffer.pointer, self.buffer.size);

        self.buffer.reset_index();

        while self.buffer.index < written_count {
            let network_event = decode_network_event_from_buffer(self.buffer);

            @dbg(network_event);

            if network_event.type == NetworkEventType::ConnectionOpen {
                self.send([1, 2, 3]);
            }
        }
    }

    send<T>(value: T) {
        let buffer = @serialize(value);

        wasm_send_message(self.websocket_id, buffer.body(), buffer.len());
    }
}

sys fn start_client() {
    CLIENT = Client::new();
    CLIENT._start();
}

sys fn update_client() {
    CLIENT._update();
    // @log(`memory used: ${get_memory_usage() / 1024} KB`);
    @retain(CLIENT);
    @trigger_garbage_collection();
}