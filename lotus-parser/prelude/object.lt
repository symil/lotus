export const TYPE_ID_TO_ANCESTOR_IDS = Map<int, Set<int>>.new();

export class Object {
    autogen static __init() {
        // @log("INIT: " + #TYPE_NAME);
        let ancestor_set = Set<int>.new();

        iter_ancestors {
            ancestor_set.add(#ANCESTOR_ID);
        }

        TYPE_ID_TO_ANCESTOR_IDS.set(#TYPE_ID, ancestor_set);
    }

    autogen static __is(object: Object) -> bool {
        let type_id = object.as_ptr()[0];

        return TYPE_ID_TO_ANCESTOR_IDS
            .get(type_id)
            .has(#TYPE_ID);
    }

    autogen dyn get_type_name() -> string {
        return #TYPE_NAME;
    }

    autogen static get_name() -> string {
        return #TYPE_NAME;
    }

    autogen static __create() -> This {
        let data = @alloc(#FIELD_COUNT + 1);

        data[0] = #TYPE_ID;

        return data as This;
    }

    autogen static default() -> This {
        let obj = This.__create();

        iter_fields {
            obj.#FIELD_NAME = #FIELD_DEFAULT_EXPRESSION;
        }

        return obj;
    }

    autogen static __none() -> This {{
        (i32.const 0)
    }}

    eq(other: This) -> bool {{
        i32.eq
    }}

    ne(other: This) -> bool {{
        i32.ne
    }}

    __is_none() -> bool {{
        (i32.eqz)
    }}

    __hash() -> int {
        return (this as int).__hash();
    }

    as_ptr() -> ptr {
        return this as ptr;
    }

    autogen dyn to_string() -> string {
        if this.__is_none() {
            return "none";
        }

        return "[" + #TYPE_SHORT_NAME + " " + (this as int).to_hexa_string() + "]";
    }

    autogen dyn to_debug_string(set: Set<ptr>) -> string {
        if this.__is_none() {
            return "none";
        }

        if !set.add(this.as_ptr()) {
            return "<cycle>";
        }

        if #FIELD_COUNT == 0 {
            return #TYPE_SHORT_NAME;
        } else {
            let str = #TYPE_SHORT_NAME + " {\n";

            iter_fields {
                str += (#FIELD_NAME + ": " + this.#FIELD_NAME.to_debug_string(set)).indent(2) + ",\n";
            }

            str += "}";

            return str;
        }
    }

    autogen dyn __retain() {
        if mem_retain(this.as_ptr()) {
            iter_fields {
                this.#FIELD_NAME.__retain();
            }
        }
    }

    autogen dyn __serialize(buffer: Buffer) {
        if buffer.register(this) {
            buffer.push(#TYPE_ID);

            iter_fields {
                this.#FIELD_NAME.__serialize(buffer);
            }
        }
    }
}